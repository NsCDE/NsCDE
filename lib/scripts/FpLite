#
# This file is a part of the NsCDE - Not so Common Desktop Environment
# Author: Hegel3DReloaded
# Licence: GPLv3
#

WindowTitle {FpLite}
WindowSize 36 34
Colorset 23

Init
Begin
   Set $CurrentIcon = {NsCDE/Fplite.s.pm}

   Key F1 A 1 3 {KeyHelp}
   Key Help A 1 3 {KeyHelp}
End

PeriodicTasks
Begin
   If (RemainderOfDiv (GetTime) 10) == 0 Then
   Begin
      Set $Getloadavg100 = (GetOutput {$NSCDE_TOOLSDIR/getla1} 1 -1)
      Set $Getloadavg10 = (GetOutput {$NSCDE_TOOLSDIR/getla1} 2 -1)
      Set $Getloadavg1 = (GetOutput {$NSCDE_TOOLSDIR/getla1} 3 -1)

      If $Getloadavg100 <= 100 Then
      Begin
         Set $IconName = {NsCDE/Fplite_lowload_} $Getloadavg10 {.s.pm}
         If $CurrentIcon <> $IconName Then
         Begin
            ChangeIcon 1 $IconName
            Set $CurrentIcon = $IconName
         End
      End

      If $Getloadavg100 > 100 Then
      Begin
         If $Getloadavg100 >= 1000 Then
         Begin
            ChangeIcon 1 NsCDE/Fplite_highload_99.s.pm
         End
         Else
         Begin
            Set $IconName = {NsCDE/Fplite_highload_} $Getloadavg1 {.s.pm}
            If $CurrentIcon <> $IconName Then
            Begin
               ChangeIcon 1 $IconName
               Set $CurrentIcon = $IconName
            End
         End
      End
   End
End

Widget 1
Property
   Position 0 0
   Size 36 34
   Type ItemDraw
   Flags NoReliefString
   Title {}
   Icon NsCDE/Fplite.s.pm
      Main
         Case message of
         SingleClic :
         Begin
            SendSignal 1 1
         End
         1 :
         Begin
            ChangeColorset 1 25
            Do {f_FpLiteClickAction}
            Do {Test (!EnvMatch infostore.fplastpressed FpLite) f_ClearFrontPanelPressed}
            Do {Schedule 50 InfoStoreAdd fplastpressed FpLite}
            Do {f_UnclickScript 450 FpLite 1 2 unclick} 
         End
         2 :
         Begin
            If (LastString) == {unclick} Then
            Begin
               ChangeColorset 1 30
            End
            If (LastString) == {unselect} Then
            Begin
               ChangeColorset 1 23
            End
         End
         3 :
         Begin
            Do {f_DisplayURL "Front Panel Lite" html/NsCDE-FpLite.html}
         End
End

